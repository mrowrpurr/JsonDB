scriptName JsonDB_Test extends SkyUnitTest
{Base script for all JsonDB tests

This destroys the test database under the TestNamespace *after* each test.

Note: for database/test cleanup to work effectively,
a JsonDB backend must be actively running. Otherwise,
you can manually delete the files generated by tests.
    
A new namespace name is provided for each test using the TestNamespace property.}

string _testNamespace
string[] _namespacesToCleanup

; Test Namespace generated for this individual test.
; Automatically deleted after the test is run.
string property TestNamespace
    string function get()
        return _testNamespace
    endFunction
endProperty

function BeforeEach()
    _testNamespace = GenerateRandomNamespaceID()
    AddNamespaceToDeleteAfterTest(_testNamespace)
endFunction

function AfterEach()
    ;;;
    ; TODO Use a blocking transaction to delete this TestNamespace
    ;;;

    _namespacesToCleanup = None
endFunction

; Get the ID for a random namespace.
; The namespace will be deleted after the test runs.
string function GetRandomTestNamespace()
    string namespace = GenerateRandomNamespaceID()
    AddNamespaceToDeleteAfterTest(namespace)
    return namespace
endFunction

; Generate a random namespace name.
; This is NOT AUTOMATICALLY DELETED AFTER THE TEST.
; Please use `GetRandomTestNamespace()` instead.
string function GenerateRandomNamespaceID()
    return "randomNamespace_" + Utility.GetCurrentRealTime() + "_" + Utility.RandomFloat(1, 1000) + "_" + Utility.RandomFloat(1, 1000)
endFunction

; Add the given namespace name to the list of namespaces to be deleted after this test runs.
; Note: Using `GetRandomTestNamespace()` does this for you! No need to do it manually.
function AddNamespaceToDeleteAfterTest(string namespace)
    if _namespacesToCleanup
        _namespacesToCleanup = Utility.ResizeStringArray(_namespacesToCleanup, _namespacesToCleanup.Length + 1)
        _namespacesToCleanup[_namespacesToCleanup.Length - 1] = namespace
    else
        _namespacesToCleanup = new string[1]
        _namespacesToCleanup[0] = namespace
    endIf
endFunction

; Returns a fully namespaced database name for the provided short, friendly database name.
; This is the same as: `JsonDB.NamespacedDB(TestNamespace, database)`
string function TestDb(string database)
    return JsonDB.NamespacedDB(TestNamespace, database)
endFunction
